## Natural Language Toolkit: feat0.fcfg
##
## First example of a feature-based grammar for English, illustrating
## value-sharing of NUM and TENSE features.
## Used in Feature-Based Grammars chapter.
## 
## Author: Ewan Klein <ewan@inf.ed.ac.uk> 
## URL: <http://nltk.sourceforge.net>
## For license information, see LICENSE.TXT

% start S
# ################### #
# Grammar Productions #
# ################### #

### S expansion productions ###
S -> S ',' Conj S
S -> S ';' S
S -> PP ',' S

# Declarative sentence
S[MOOD=decl] -> NP[NUM=?n, PERS=?p] VP[NUM=?n, PERS?=p]
# Yes/no question
S[MOOD=ynq] -> Aux NP[NUM=?n] VP[NUM=?n]
# Wh-question
S[MOOD=whq] -> Wh Aux NP[NUM=?n] VP[NUM=?n]
#Imperative
S[MOOD=imper] -> VP[TENSE=infinitive]


### Clause expansion productions ###
Clause[TYPE=s] -> S[MOOD=decl]
Clause[TYPE=s] -> Compl S[MOOD=decl]
Clause[TYPE=s] -> Compl VP[TENSE=passive_past]
Clause[TYPE=vp_inf] -> VP[TENSE=infinitive]
Clause[TYPE=wh] -> Wh VP


### NP expansion productions ###
NP[NUM=pl, PERS=3] -> N[NUM=pl] 
NP[NUM=sg, PERS=3] -> N[MASS=1] 
NP[NUM=?n, PERS=3] -> PropN[NUM=?n, NEED_DET=0]
NP[NUM=?n, PERS=3] -> Det[NUM=?n] PropN[NUM=?n] 
NP[NUM=?n, PERS=3] -> Det[NUM=?n] N[NUM=?n]
NP[NUM=?n, PERS=3] -> ProN[NUM=?n]
NP[NUM=?n, PERS=?p] -> RflexProN[NUM=?n, PERS=?p]
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] PP
NP[NUM=?n, PERS=?p] -> Det[NUM=?n] AdjP N[NUM=?n]
NP[NUM=pl, PERS=?p] -> AdjP N[NUM=pl] 
NP[NUM=?n, PERS=?p] -> AdjP N[NUM=?n, MASS=1]
NP[NUM=?n, PERS=?p] -> AdjP PropN[NUM=?n, NEED_DET=0]
NP[NUM=pl, PERS=3] -> NP Conj NP
NP[NUM=pl, PERS=3] -> NP ',' Conj NP ','
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' NP[NUM=?n, PERS=?p] 
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' NP[NUM=?n, PERS=?p] ','
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] Clause[TYPE=s]
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' Clause[TYPE=wh]
NP[NUM=?n, PERS=3] -> CD
NP[NUM=?n, PERS=?p] -> Det[NUM=?n] '"' NP[NUM=?n, PERS=?p] '"'


### VP expansion productions ###
VP[TENSE=?t, NUM=?n] -> AdvP VP[TENSE=?t, NUM=?n] 
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n]
VP[TENSE=?t, NUM=?n] -> VP[TENSE=?t, NUM=?n] Conj VP[TENSE=?t, NUM=?n]

VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=np] NP
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=pp] PP
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=cl] Clause[TYPE=s]
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=adj] AdjP

VP[TENSE=?t, NUM=?n] -> DTV[TENSE=?t, NUM=?n, ARG1=np, ARG2=pp] NP PP
VP[TENSE=?t, NUM=?n] -> DTV[TENSE=?t, NUM=?n, ARG1=pp, ARG2=pp] PP PP

VP[TENSE=?t, NUM=?n] -> TTV[TENSE=?t, NUM=?n, ARG1=np, ARG2=pp, ARG3=pp] PP PP PP


## Tense: Simple Future ##
IV[TENSE=simple_future, VOICE=active] -> 'will' IV[TENSE=infinitive]
TV[TENSE=simple_future, VOICE=active, ARG1=?a1] -> 'will' TV[TENSE=infinitive, ARG1=?a1]
DTV[TENSE=simple_future, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'will' DTV[TENSE=infinitive, ARG1=?a1, ARG2=?a2]

## Tense: Present Perfect ##
IV[TENSE=pres_perf, VOICE=active] -> 'has' IVBar[TENSE=past_participle]
TV[TENSE=pres_perf, VOICE=active, ARG1=?a1] -> 'has' TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=pres_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'has' DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Future Perfect ##
IV[TENSE=future_perf, VOICE=active] -> 'will' 'have' IVBar[TENSE=past_participle]
TV[TENSE=future_perf, VOICE=active, ARG1=?a1] -> 'will' 'have' TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=future_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'will' 'have' DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Past Perfect ##
IV[TENSE=past_perf, VOICE=active] -> 'had' IVBar[TENSE=past_participle]
TV[TENSE=past_perf, VOICE=active, ARG1=?a1] -> 'had' TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=past_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'had' DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Present Progressive ##
IV[TENSE=pres_progressive, VOICE=active] -> 'is' IVBar[TENSE=pres_participle]
TV[TENSE=pres_progressive, VOICE=active, ARG1=?a1] -> 'is' TVBar[TENSE=pres_participle, ARG1=?a1]
DTV[TENSE=pres_progressive, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'is' DTVBar[TENSE=pres_participle, ARG1=?a1, ARG2=?a2]

## Tense: Past Progressive ##
IV[TENSE=past_progressive, VOICE=active] -> 'was' IVBar[TENSE=pres_participle]
TV[TENSE=past_progressive, VOICE=active, ARG1=?a1] -> 'was' TVBar[TENSE=pres_participle, ARG1=?a1]
DTV[TENSE=past_progressive, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'was' DTVBar[TENSE=pres_participle, ARG1=?a1, ARG2=?a2]

## Passive voice ##
IV[TENSE=passive_past, VOICE=passive] -> AuxPass IVBar[TENSE=past_participle]
TV[TENSE=passive_past, VOICE=passive, ARG1=?a1] -> AuxPass TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=passive_past, VOICE=passive, ARG1=?a1, ARG2=?a2] -> AuxPass DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

# ADDING ADVERBS to PASSIVE VOICE
IV[TENSE=passive_past, VOICE=passive] -> AuxPass AdvP IVBar[TENSE=past_participle]
TV[TENSE=passive_past, VOICE=passive, ARG1=?a1] -> AuxPass AdvP TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=passive_past, VOICE=passive, ARG1=?a1, ARG2=?a2] -> AuxPass Adv DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]


### PP expansion productions ###
PP -> P NP
PP -> P VP
PP -> P S[MOOD=decl]

### AdjP expansion productions ###
AdjP -> NP
AdjP -> CD
AdjP -> AdjP AdjP
AdjP -> Adj
AdjP -> AdjP Conj AdjP
AdjP -> Adj ',' Adj 'and' Adj
AdjP -> NP POSS
AdjP -> Neg AdjP
AdjP -> '"' AdjP '"'
AdjP -> AdjP PP

### Adverb expansion productions ###
AdvP -> Adv

# ################### #
# Lexical Productions #
# ################### #

### Determiners ###
Det[NUM=sg] -> 'this' | 'every' | 'a' | 'an'
Det[NUM=pl] -> 'these' | 'all' |  'several' | 'some' 
Det -> 'the' | 'her' | 'his' | 'its' | 'their'


### Conjunctions ###
Conj -> 'and' | 'but' | 'or' | 'after'


### Possessive Marker ###
POSS -> "'s"


### Complementizers ###
Compl -> 'that'


### Wh-words ###
Wh -> 'who' | 'what' | 'where' | 'when' | 'why' | 'how'

### Negation ###
Neg -> 'not'

### Prepositions ###
P -> 'above' | 'across' | 'after' | 'at' | 'around' | 'before' | 'behind' | 'below' | 'beside' | 'between' | 'by' 
P -> 'down' | 'during' | 'for' | 'from' | 'in' | 'inside' | 'near' | 'onto' | 'of' | 'off' | 'on' | 'out' | 'over'
P ->  'through' | 'to' | 'under' | 'up' | 'with'


### Adjectives ###
CD -> '87' | '11pm' | 'one' | 'two' | 'seven' | '17' | '135' | '1655' | '7' | '1985'

Adj -> 'Muslim' | 'chronic' | 'physical' | 'mental' | 'government' | 'Australian' | 'outgoing' | 'new' | 'unnecessary' | 'youngest'
Adj -> 'political' | 'local' | 'Shiite' | 'Kurdish' | 'Sunni' | 'fictional' | 'sci-fi' | 'German' | 'retail' | 'discount'
Adj -> 'Arab' | 'medical' | 'high' | 'Indian-born' | 'least' | 'at' 'least' | 'combatant' | 'Dutch' | 'former' | 'open' | 'forthright' | 'own'


### Adverbs ###
Adv -> 'also' | 'soon' | 'quickly' 


### Pronouns ###
ProN[NUM=sg, PERS=1] -> 'I'
ProN[NUM=pl, PERS=1] -> 'we'
ProN[PERS=2] -> 'you'
ProN[NUM=sg, PERS=3, GEN=masc] -> 'he'
ProN[NUM=sg, PERS=3, GEN=fem] -> 'she'
ProN[NUM=sg, PERS=3] -> 'it'
ProN[NUM=pl, PERS=3] -> 'they'

RflexProN[NUM=sg, PERS=1] -> 'myself'
RflexProN[NUM=sg, PERS=2] -> 'yourself'
RflexProN[NUM=pl, PERS=2] -> 'yourselves'
RflexProN[NUM=sg, PERS=3, GEN=masc] -> 'himself'
RflexProN[NUM=sg, PERS=3, GEN=fem] -> 'herself'
RflexProN[NUM=sg, PERS=3] -> 'themself'
RflexProN[NUM=pl, PERS=3] -> 'themselves'


### Proper Nouns ###
# Do not need to be preceded by determiner
PropN[NUM=sg, NEED_DET=0] -> 'Kim' | 'Jody' | 'Isaac' 'Asimov' | 'Preem' 'Palver' | 'White' | 'Voss' | 'Becker'
PropN[NUM=sg, NEED_DET=0] -> 'Saint' 'Joan' 'of' 'Arc' | 'Harry' 'M' 'Miller' | 'Jerry' 'Reinsdorf' | 'Bush'
PropN[NUM=sg, NEED_DET=0] -> 'Dan' 'Collins' | 'Habib' | 'Edward' 'Philips' | 'Toni' 'Hoffman' | 'Dr' 'Patel'
PropN[NUM=sg, NEED_DET=0] -> 'Jayant' 'Patel' | 'Kaew' 'Panjapetchkaew' | 'Toby' 'Gascon' | 'Huygens'
PropN[NUM=sg, NEED_DET=0] -> 'Christiaan' 'Huygens' | 'Iyad' 'Allawi' | 'Mr' 'Kennedy' | 'Laurie' 'Garner'
PropN[NUM=sg, NEED_DET=0] -> 'Mr' 'Margulies' | 'Mr' 'Habib' | 'Fathiya' 'Barghouti' 'Rheime' | 'Dr' 'Jayant' 'Patel'
PropN[NUM=sg, NEED_DET=0] -> 'Mr' 'McClellan' | 'Mr' 'Bush' | 'Scott' 'McClellan' | 'George' 'W' 'Bush' | 'Boris' 'Franz' 'Becker'

PropN[NUM=sg, NEED_DET=0] -> 'Al' 'Jazeera' | '1st' 'Marine' 'Division' | 'Google' | 'Time' 'Warner' 
PropN[NUM=sg, NEED_DET=0] -> 'Minnesota' 'Gophers' | 'Boeing' | 'Greenpeace' | 'ESA' | 'July'
PropN[NUM=sg, NEED_DET=0] -> 'September' '11' | 'Harvard' 'University' | 'Prime' 'Minister' | 'Wimbledon'
PropN[NUM=sg, NEED_DET=0] -> 'Wednesday' | '1st' 'Marine' 'division' | 'Hamas' | 'Hurricane' 'Katrina' | 'President' 

PropN[NUM=sg, NEED_DET=0] -> 'Rewe' | 'Germany' | 'Medan' | 'Malaysia' | 'Gaza' | 'Ahvaz' | 'Rutbah' | 'North' 'Carolina'
PropN[NUM=sg, NEED_DET=0] -> 'Iraq' | 'Australia' | 'Penny' 'Market' | 'Romania' | 'US' | 'Titan' | 'Japan' | 'White' 'House' 

PropN[NUM=pl, NEED_DET=0] -> 'Muslims' | 'Iranians'

# Must be preceded by determiner
PropN[NUM=sg, NEED_DET=1, PERS=3] -> 'Foundation' 'Series' | 'Best' 'Party' 'of' 'Allah' | 'Australian' 'Capital' 'Territory'
PropN[NUM=pl, NEED_DET=1, PERS=3] -> 'Houston' 'Astros' | 'White' 'Sox' | 'Muslims' 


### Nouns ###
# Singular
N[NUM=sg, PERS=3] -> 'dog' | 'girl' | 'car' | 'child' | 'screenplay' | 'aircraft' | 'owner' | 'subsidiary' | 'woman'
N[NUM=sg, PERS=3] -> 'monk' | 'brigade' | 'division' | 'inquiry' | 'inventor' | 'probe' | 'bomb' | 'attacker'
N[NUM=sg, PERS=3] -> 'citizen' | 'night' | 'party' | 'character' | 'part' | 'writer' | 'insurgency' | 'team' | 'corporation'
N[NUM=sg, PERS=3] -> 'year' | 'corporation' | 'hockey' | 'assassin' | "'suspension'" | 'city' | '30-year-old' | 'school'
N[NUM=sg, PERS=3] -> 'teacher' | 'mother' | 'strip' | 'course' | 'mission' | 'astronomer' | 'champion'
N[NUM=sg, PERS=3] -> 'book' | 'campaign' | 'war' | 'spokesman' | 'intervention' | 'role' | 'stage' | 'call' | 'player' | 'history'

# Plural
N[NUM=pl, PERS=3] -> 'dogs' | 'girls' | 'cars' | 'children' | 'personnel' | 'attacks' | 'people' | 'men' | 'protests' | 'countries' | 'forces'
N[NUM=pl, PERS=3] -> 'wounded' | 'conditions' | 'leaders' | 'stores' | 'women' | 'mayors' | 'deaths' | 'years' | 'members' | 'weapons' | 'provisions' | 'singles'

# Exception
N[PERS=3] -> 'mayor' | 'world'

# Mass nouns
N[NUM=sg, PERS=3, MASS=1] -> 'cancer' | 'incarceration' | 'time' | 'police' | 'nothing' | 'middle' | 'night' | 'treatment' | 'money' | 'information'
N[NUM=sg, PERS=3, MASS=1] -> 'tennis' | 'handling' | 'propaganda' | 'military' | 'freedom' | 'tyranny' | 'tennis' | 'history'

### Passive Auxiliary ###
AuxPass -> 'should' | 'was' | 'is' | 'were' | 'is' 'being' | 'have'


### Verbs ###

## Tense: Simple Present ##
IV[TENSE=simple_pres, NUM=sg] -> 'disappears' | 'walks'

TV[TENSE=simple_pres, NUM=sg, ARG1=np] -> 'is' | 'causes' | 'needs'
TV[TENSE=simple_pres, NUM=sg, ARG1=pp] -> 'is' | 'resides' | 'plans' | 'works' | 'belongs' | 'comes' | 'suffers'
TV[TENSE=simple_pres, NUM=sg, ARG1=cl] -> 'says'

IV[TENSE=simple_pres, NUM=pl] -> 'disappear' | 'walk'
TV[TENSE=simple_pres, NUM=pl, ARG1=np] -> 'play' | 'are' | 'represent' | 'back' | 'open'
DTV[TENSE=simple_pres, NUM=pl, ARG1=np, ARG2=pp] -> 'obtain'
TV[TENSE=simple_pres, NUM=pl, ARG1=cl] -> 'claim'

## Tense: Simple Past ##
IV[TENSE=simple_past] -> 'disappeared' | 'walked' 

TV[TENSE=simple_past, ARG1=np] -> 'invented' | 'died'  | 'killed' | 'was' | 'assisted' | 'defeated' | 'came' 'of' | 'incited' | 'praised'
TV[TENSE=simple_past, ARG1=pp] -> 'participated' | 'died' | 'worked' | 'killed' | 'was' | 'named' | 'elected' | 'protested' | 'linked' | 'began' | 'grew' | 'cared' | 'rushed'
TV[TENSE=simple_past, ARG1=adj] -> 'was'

DTV[TENSE=simple_past, ARG1=np, ARG2=pp] -> 'wrote' | 'named' | 'elected' | 'discovered' | 'became'
DTV[TENSE=simple_past, ARG1=pp, ARG2=pp] -> 'relied'


TTV[TENSE=simple_past, ARG1=np, ARG2=pp, ARG3=pp] -> 'escaped'


## Tense: Infinitive ##
IV[TENSE=infinitive] -> 'disappear' | 'walk' 
TV[TENSE=infinitive, ARG1=pp] -> 'work' 
TV[TENSE=infinitive, ARG1=np] -> 'sell' | 'combat'


## Past Participle ##
IVBar[TENSE=past_participle] -> 'disappeared' | 'walked' | 'attacked'

TVBar[TENSE=past_participle, ARG1=np] -> 'recommended' | 'elected'
TVBar[TENSE=past_participle, ARG1=pp] -> 'based' | 'built' | 'killed' | 'named' | 'criticised' 
TVBar[TENSE=past_participle, ARG1=cl] -> 'said'

DTVBar[TENSE=past_participle, ARG1=np, ARG2=pp] -> 'wrote' | 'backed'
DTVBar[TENSE=past_participle, ARG1=pp, ARG2=pp] -> 'approached' | 'formed' | 'linked'

## Present Participle ##
IVBar[TENSE=pres_participle] -> 'disappearing' | 'walking' 

DTVBar[TENSE=pres_participle, ARG1=np, ARG2=pp] -> 'transporting' 

