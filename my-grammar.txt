## Natural Language Toolkit: feat0.fcfg
##
## First example of a feature-based grammar for English, illustrating
## value-sharing of NUM and TENSE features.
## Used in Feature-Based Grammars chapter.
## 
## Author: Ewan Klein <ewan@inf.ed.ac.uk> 
## URL: <http://nltk.sourceforge.net>
## For license information, see LICENSE.TXT

% start S
# ################### #
# Grammar Productions #
# ################### #


### S expansion productions ###
S -> S ',' Conj S
S -> S ';' S
S -> PP ',' S

# Declarative sentence
S[MOOD=decl] -> NP[NUM=?n, PERS=?p] VP[NUM=?n, PERS?=p]
# Yes/no question
S[MOOD=ynq] -> Aux NP[NUM=?n] VP[NUM=?n]
# Wh-question
S[MOOD=whq] -> Wh Aux NP[NUM=?n] VP[NUM=?n]
#Imperative
S[MOOD=imper] -> VP[TENSE=infinitive]


### Clause expansion productions ###
Clause[TYPE=s] -> S[MOOD=decl]
Clause[TYPE=relative] -> ProN[TYPE=relative] S[MOOD=decl]
Clause[TYPE=relative] -> ProN[TYPE=relative] VP
Clause[TYPE=vp_inf] -> VP[TENSE=infinitive]



### NP expansion productions ###

NP[NUM=pl, PERS=3] -> Nom[NUM=pl] 
NP[NUM=sg, PERS=3] -> Nom[MASS=1] 
NP[NUM=?n, PERS=3] -> PropN[NUM=?n, NEED_DET=0]
NP[NUM=pl, PERS=?p] -> AdjP Nom[NUM=pl] 
NP[NUM=?n, PERS=?p] -> AdjP Nom[NUM=?n, MASS=1]
NP[NUM=?n, PERS=?p] -> AdjP PropN[NUM=?n, NEED_DET=0]

NP[NUM=?n, PERS=3] -> ProN[TYPE=subj, NUM=?n]
NP[NUM=?n, PERS=?p] -> ProN[TYPE=rflex, NUM=?n, PERS=?p]
NP[NUM=?n, PERS=3] -> CD

NP[NUM=?n, PERS=3] -> Det[NUM=?n] PropN[NUM=?n] 
NP[NUM=?n, PERS=3] -> Det[NUM=?n] Nom[NUM=?n]
NP[NUM=?n, PERS=?p] -> Det[NUM=?n] '"' NP[NUM=?n, PERS=?p] '"'
NP[NUM=?n, PERS=?p] -> Det[NUM=?n] AdjP Nom[NUM=?n, PERS=?p]

## Conjoined NPs
NP[NUM=pl, PERS=3] -> NP Conj NP
NP[NUM=pl, PERS=3] -> NP ',' Conj NP ','
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' NP[NUM=?n, PERS=?p] 
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' NP[NUM=?n, PERS=?p] ','
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] Clause[TYPE=s]
NP[NUM=?n, PERS=?p] -> NP[NUM=?n, PERS=?p] ',' Clause[TYPE=wh]



### Nom expansion productions ###
Nom[NUM=?n, PERS=?p] -> Nom[NUM=?n, PERS=?p] PP
Nom[NUM=?n, PERS=3] -> Noun Nom[NUM=?n]
Nom[NUM=?n, PERS=3] -> Noun[NUM=?n]
Nom[NUM=?n, PERS=?p] -> Nom[NUM=?n, PERS=?p] VP[TENSE=gerund]
Nom[NUM=?n, PERS=?p] -> Nom[NUM=?n, PERS=?p] Clause[TYPE=relative]



### VP expansion productions ###
VP[TENSE=?t, NUM=?n] -> AdvP VP[TENSE=?t, NUM=?n] 
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n]
VP[TENSE=?t, NUM=?n] -> VP[TENSE=?t, NUM=?n] Conj VP[TENSE=?t, NUM=?n]

VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=np] NP
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=pp] PP
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=cl] Clause[TYPE=s]
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n, ARG1=adj] AdjP

VP[TENSE=?t, NUM=?n] -> DTV[TENSE=?t, NUM=?n, ARG1=np, ARG2=pp] NP PP
VP[TENSE=?t, NUM=?n] -> DTV[TENSE=?t, NUM=?n, ARG1=pp, ARG2=pp] PP PP

VP[TENSE=?t, NUM=?n] -> TTV[TENSE=?t, NUM=?n, ARG1=np, ARG2=pp, ARG3=pp] PP PP PP

## Auxiliary Verbs ##
Aux[FORM=be, TENSE=present] -> 'is'
Aux[FORM=be, TENSE=past] -> 'was'
Aux[FORM=be, TENSE=inf] -> 'be'
Aux[FORM=be, TENSE=past_participle] -> 'been'
Aux[FORM=have, TENSE=present] -> 'has'
Aux[FORM=have, TENSE=past] -> 'had'
Aux[FORM=have, TENSE=inf] -> 'have'

# Passive Auxiliary ###
Aux[TYPE=passive] -> 'should' | 'was' | 'is' | 'were' | 'is' 'being' | 'have'

## Modal Verbs ##
Modal[FORM=will] -> 'will'
Modal[FORM=can] -> 'can'
Modal[FORM=could] -> 'could'


## Tense: Simple Future ##
IV[TENSE=simple_future, VOICE=active] -> Modal[FORM=will] IV[TENSE=infinitive]
TV[TENSE=simple_future, VOICE=active, ARG1=?a1] -> Modal[FORM=will] TV[TENSE=infinitive, ARG1=?a1]
DTV[TENSE=simple_future, VOICE=active, ARG1=?a1, ARG2=?a2] -> Modal[FORM=will] DTV[TENSE=infinitive, ARG1=?a1, ARG2=?a2]

## Tense: Present Perfect ##
IV[TENSE=pres_perf, VOICE=active] -> 'has' IVBar[TENSE=past_participle]
TV[TENSE=pres_perf, VOICE=active, ARG1=?a1] -> 'has' TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=pres_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> 'has' DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Future Perfect ##
IV[TENSE=future_perf, VOICE=active] -> Modal[FORM=will] Aux[FORM=have, TENSE=inf] IVBar[TENSE=past_participle]
TV[TENSE=future_perf, VOICE=active, ARG1=?a1] -> Modal[FORM=will] Aux[FORM=have, TENSE=inf] TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=future_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> Modal[FORM=will] Aux[FORM=have, TENSE=inf] DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Past Perfect ##
IV[TENSE=past_perf, VOICE=active] -> Aux[FORM=have, TENSE=past] IVBar[TENSE=past_participle]
TV[TENSE=past_perf, VOICE=active, ARG1=?a1] -> Aux[FORM=have, TENSE=past] TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=past_perf, VOICE=active, ARG1=?a1, ARG2=?a2] -> Aux[FORM=have, TENSE=past] DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

## Tense: Present Progressive ##
IV[TENSE=pres_progressive, VOICE=active] -> Aux[FORM=be, TENSE=present] IVBar[TENSE=pres_participle]
TV[TENSE=pres_progressive, VOICE=active, ARG1=?a1] -> Aux[FORM=be, TENSE=present] TVBar[TENSE=pres_participle, ARG1=?a1]
DTV[TENSE=pres_progressive, VOICE=active, ARG1=?a1, ARG2=?a2] -> Aux[FORM=be, TENSE=present] DTVBar[TENSE=pres_participle, ARG1=?a1, ARG2=?a2]

## Tense: Past Progressive ##
IV[TENSE=past_progressive, VOICE=active] -> Aux[FORM=be, TENSE=past] IVBar[TENSE=pres_participle]
TV[TENSE=past_progressive, VOICE=active, ARG1=?a1] -> Aux[FORM=be, TENSE=past] TVBar[TENSE=pres_participle, ARG1=?a1]
DTV[TENSE=past_progressive, VOICE=active, ARG1=?a1, ARG2=?a2] -> Aux[FORM=be, TENSE=past] DTVBar[TENSE=pres_participle, ARG1=?a1, ARG2=?a2]

## Tense: Gerundive ##
IV[TENSE=gerund] -> IVBar[TENSE=pres_participle]
TV[TENSE=gerund] -> TVBar[TENSE=pres_participle]
DTV[TENSE=gerund] -> DTVBar[TENSE=pres_participle]


## Passive voice ##
IV[TENSE=passive_past, VOICE=passive] -> Aux[TYPE=passive] IVBar[TENSE=past_participle]
TV[TENSE=passive_past, VOICE=passive, ARG1=?a1] -> Aux[TYPE=passive] TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=passive_past, VOICE=passive, ARG1=?a1, ARG2=?a2] -> Aux[TYPE=passive] DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]

# ADDING ADVERBS to PASSIVE VOICE
IV[TENSE=passive_past, VOICE=passive] -> Aux[TYPE=passive] AdvP IVBar[TENSE=past_participle]
TV[TENSE=passive_past, VOICE=passive, ARG1=?a1] -> Aux[TYPE=passive] AdvP TVBar[TENSE=past_participle, ARG1=?a1]
DTV[TENSE=passive_past, VOICE=passive, ARG1=?a1, ARG2=?a2] -> Aux[TYPE=passive] Adv DTVBar[TENSE=past_participle, ARG1=?a1, ARG2=?a2]


### PP expansion productions ###
PP -> P NP
PP -> P VP
PP -> P S[MOOD=decl]

### AdjP expansion productions ###
AdjP -> AdjP AdjP
AdjP -> Adj
AdjP -> AdjP Conj AdjP
AdjP -> Adj ',' Adj 'and' Adj
AdjP -> NP POSS
AdjP -> Neg AdjP
AdjP -> '"' AdjP '"'
AdjP -> AdjP PP

### Adverb expansion productions ###
AdvP -> Adv

# ################### #
# Lexical Productions #
# ################### #

### Determiners ###
Det[NUM=sg] -> 'this' | 'every' | 'a' | 'an'
Det[NUM=pl] -> 'these' | 'all' |  'several' | 'some' 
Det -> 'the' | 'her' | 'his' | 'its' | 'their'


### Conjunctions ###
Conj -> 'and' | 'but' | 'or' | 'after'


### Possessive Marker ###
POSS -> "'s"


### Wh-words ###
Wh -> 'who' | 'what' | 'where' | 'when' | 'why' | 'how'


### Negation ###
Neg -> 'not'


### Prepositions ###
P -> 'above' | 'across' | 'after' | 'at' | 'around' | 'before' | 'behind' | 'below' | 'beside' | 'between' | 'by' 
P -> 'down' | 'during' | 'for' | 'from' | 'in' | 'inside' | 'near' | 'onto' | 'of' | 'off' | 'on' | 'out' | 'over'
P ->  'through' | 'to' | 'under' | 'up' | 'with'


### Cardinal Numbers ###
CD -> '87' | '11pm' | 'one' | 'two' | 'seven' | '17' | '135' | '1655' | '7' | '1985'

### Adjectives ###
Adj -> 'chronic' | 'physical' | 'mental' | 'outgoing' | 'new' | 'unnecessary' | 'youngest'| 'political' | 'local' 
Adj -> 'fictional' | 'medical' | 'high' | 'least' | 'at' 'least' | 'former' | 'open' | 'forthright' 


### Adverbs ###
Adv -> 'also' | 'soon' | 'quickly' 


### Pronouns ###
## Subject type
ProN[TYPE=subj, NUM=sg, PERS=1] -> 'I'
ProN[TYPE=subj, NUM=pl, PERS=1] -> 'we'
ProN[TYPE=subj, PERS=2] -> 'you'
ProN[TYPE=subj, NUM=sg, PERS=3, GEN=masc] -> 'he'
ProN[TYPE=subj, NUM=sg, PERS=3, GEN=fem] -> 'she'
ProN[TYPE=subj, NUM=sg, PERS=3] -> 'it'
ProN[TYPE=subj, NUM=pl, PERS=3] -> 'they'

## Reflexive type
ProN[TYPE=rflex, NUM=sg, PERS=1] -> 'myself'
ProN[TYPE=rflex, NUM=sg, PERS=2] -> 'yourself'
ProN[TYPE=rflex, NUM=pl, PERS=2] -> 'yourselves'
ProN[TYPE=rflex, NUM=sg, PERS=3, GEN=masc] -> 'himself'
ProN[TYPE=rflex, NUM=sg, PERS=3, GEN=fem] -> 'herself'
ProN[TYPE=rflex, NUM=sg, PERS=3] -> 'themself'
ProN[TYPE=rflex, NUM=pl, PERS=3] -> 'themselves'

## Relative type
ProN[TYPE=relative] -> 'that' | 'who'

### Proper Nouns ###
# Do not need to be preceded by determiner
PropN[NUM=sg, NEED_DET=0] -> 'Kim' | 'Jody' | 'Isaac' 'Asimov' | 'Preem' 'Palver' | 'White' | 'Voss' | 'Becker'
PropN[NUM=sg, NEED_DET=0] -> 'Saint' 'Joan' 'of' 'Arc' | 'Harry' 'M' 'Miller' | 'Jerry' 'Reinsdorf' | 'Bush'
PropN[NUM=sg, NEED_DET=0] -> 'Dan' 'Collins' | 'Habib' | 'Edward' 'Philips' | 'Toni' 'Hoffman' | 'Dr' 'Patel'
PropN[NUM=sg, NEED_DET=0] -> 'Jayant' 'Patel' | 'Kaew' 'Panjapetchkaew' | 'Toby' 'Gascon' | 'Huygens'
PropN[NUM=sg, NEED_DET=0] -> 'Christiaan' 'Huygens' | 'Iyad' 'Allawi' | 'Mr' 'Kennedy' | 'Laurie' 'Garner'
PropN[NUM=sg, NEED_DET=0] -> 'Mr' 'Margulies' | 'Mr' 'Habib' | 'Fathiya' 'Barghouti' 'Rheime' | 'Dr' 'Jayant' 'Patel'
PropN[NUM=sg, NEED_DET=0] -> 'Mr' 'McClellan' | 'Mr' 'Bush' | 'Scott' 'McClellan' | 'George' 'W' 'Bush' | 'Boris' 'Franz' 'Becker'

PropN[NUM=sg, NEED_DET=0] -> 'Al' 'Jazeera' | '1st' 'Marine' 'Division' | 'Google' | 'Time' 'Warner' 
PropN[NUM=sg, NEED_DET=0] -> 'Minnesota' 'Gophers' | 'Boeing' | 'Greenpeace' | 'ESA' | 'July'
PropN[NUM=sg, NEED_DET=0] -> 'September' '11' | 'Harvard' 'University' | 'Prime' 'Minister' | 'Wimbledon'
PropN[NUM=sg, NEED_DET=0] -> 'Wednesday' | '1st' 'Marine' 'division' | 'Hamas' | 'Hurricane' 'Katrina' | 'President' 

PropN[NUM=sg, NEED_DET=0] -> 'Rewe' | 'Germany' | 'Medan' | 'Malaysia' | 'Gaza' | 'Ahvaz' | 'Rutbah' | 'North' 'Carolina'
PropN[NUM=sg, NEED_DET=0] -> 'Iraq' | 'Australia' | 'Penny' 'Market' | 'Romania' | 'US' | 'Titan' | 'Japan' | 'White' 'House' 

PropN[NUM=pl, NEED_DET=0] -> 'Muslims' | 'Iranians'

# Must be preceded by determiner
PropN[NUM=sg, NEED_DET=1, PERS=3] -> 'Foundation' 'Series' | 'Best' 'Party' 'of' 'Allah' | 'Australian' 'Capital' 'Territory'
PropN[NUM=pl, NEED_DET=1, PERS=3] -> 'Houston' 'Astros' | 'White' 'Sox' | 'Muslims' 


### Nouns ###
# Singular
Noun[NUM=sg, PERS=3] -> 'dog' | 'girl' | 'car' | 'child' | 'screenplay' | 'aircraft' | 'owner' | 'subsidiary' | 'woman'
Noun[NUM=sg, PERS=3] -> 'monk' | 'brigade' | 'division' | 'inquiry' | 'inventor' | 'probe' | 'bomb' | 'attacker'
Noun[NUM=sg, PERS=3] -> 'citizen' | 'night' | 'party' | 'character' | 'part' | 'writer' | 'insurgency' | 'team' | 'corporation'
Noun[NUM=sg, PERS=3] -> 'year' | 'corporation' | 'hockey' | 'assassin' | "'suspension'" | 'city' | '30-year-old' | 'school'
Noun[NUM=sg, PERS=3] -> 'teacher' | 'mother' | 'strip' | 'course' | 'mission' | 'astronomer' | 'champion'
Noun[NUM=sg, PERS=3] -> 'book' | 'campaign' | 'war' | 'spokesman' | 'intervention' | 'role' | 'stage' | 'call' | 'player' | 'history'

# Plural
Noun[NUM=pl, PERS=3] -> 'dogs' | 'girls' | 'cars' | 'children' | 'personnel' | 'attacks' | 'people' | 'men' | 'protests' | 'countries' | 'forces'
Noun[NUM=pl, PERS=3] -> 'wounded' | 'conditions' | 'leaders' | 'stores' | 'women' | 'mayors' | 'deaths' | 'years' | 'members' | 'weapons' | 'provisions' | 'singles'

# Exception
Noun[PERS=3] -> 'mayor' | 'world'

# Mass nouns
Noun[NUM=sg, PERS=3, MASS=1] -> 'cancer' | 'incarceration' | 'time' | 'police' | 'nothing' | 'middle' | 'night' | 'treatment' | 'money' | 'information'
Noun[NUM=sg, PERS=3, MASS=1] -> 'tennis' | 'handling' | 'propaganda' | 'military' | 'freedom' | 'tyranny' | 'tennis' | 'history'




### Verbs ###

## Tense: Simple Present ##
IV[TENSE=simple_pres, NUM=sg] -> 'disappears' | 'walks'

TV[TENSE=simple_pres, NUM=sg, ARG1=np] -> 'is' | 'causes' | 'needs'
TV[TENSE=simple_pres, NUM=sg, ARG1=pp] -> 'is' | 'resides' | 'plans' | 'works' | 'belongs' | 'comes' | 'suffers'
TV[TENSE=simple_pres, NUM=sg, ARG1=cl] -> 'says'

IV[TENSE=simple_pres, NUM=pl] -> 'disappear' | 'walk'
TV[TENSE=simple_pres, NUM=pl, ARG1=np] -> 'play' | 'are' | 'represent' | 'back' | 'open'
DTV[TENSE=simple_pres, NUM=pl, ARG1=np, ARG2=pp] -> 'obtain'
TV[TENSE=simple_pres, NUM=pl, ARG1=cl] -> 'claim'

## Tense: Simple Past ##
IV[TENSE=simple_past] -> 'disappeared' | 'walked' 

TV[TENSE=simple_past, ARG1=np] -> 'invented' | 'died'  | 'killed' | 'was' | 'assisted' | 'defeated' | 'came' 'of' | 'incited' | 'praised'
TV[TENSE=simple_past, ARG1=pp] -> 'participated' | 'died' | 'worked' | 'killed' | 'was' | 'named' | 'elected' | 'protested' | 'linked' | 'began' | 'grew' | 'cared' | 'rushed'
TV[TENSE=simple_past, ARG1=adj] -> 'was'

DTV[TENSE=simple_past, ARG1=np, ARG2=pp] -> 'wrote' | 'named' | 'elected' | 'discovered' | 'became'
DTV[TENSE=simple_past, ARG1=pp, ARG2=pp] -> 'relied'


TTV[TENSE=simple_past, ARG1=np, ARG2=pp, ARG3=pp] -> 'escaped'


## Tense: Infinitive ##
IV[TENSE=infinitive] -> 'disappear' | 'walk' 
TV[TENSE=infinitive, ARG1=pp] -> 'work' 
TV[TENSE=infinitive, ARG1=np] -> 'sell' | 'combat'


## Past Participle ##
IVBar[TENSE=past_participle] -> 'disappeared' | 'walked' | 'attacked'

TVBar[TENSE=past_participle, ARG1=np] -> 'recommended' | 'elected'
TVBar[TENSE=past_participle, ARG1=pp] -> 'based' | 'built' | 'killed' | 'named' | 'criticised' 
TVBar[TENSE=past_participle, ARG1=cl] -> 'said'

DTVBar[TENSE=past_participle, ARG1=np, ARG2=pp] -> 'wrote' | 'backed'
DTVBar[TENSE=past_participle, ARG1=pp, ARG2=pp] -> 'approached' | 'formed' | 'linked'

## Present Participle ##
IVBar[TENSE=pres_participle] -> 'disappearing' | 'walking' 

DTVBar[TENSE=pres_participle, ARG1=np, ARG2=pp] -> 'transporting' 

